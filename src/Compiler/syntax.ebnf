block = 
    | {line "."}

line = 
    | list ":-" list
    | list

list =
    | {process ","} process
    | Null

creation =
    | {"\" {linkName " "} + "." } process    

process =
    | atom0             (* starting from AtomName *)
      			(* the weakest operator precedence *)
    | indirection       (* starting from PointerName *)
    | "(" line ")"      (* starting from opening parentheses *)

atom0 = processContext ":=" atom4 | atom4
atom4 = 
    | atom5 "="  atom5
    | atom5 "/=" atom5
    | atom5 "<"  atom5
    | atom5 ">"  atom5
    | atom5 "<=" atom5
    | atom5 ">=" atom5
    | atom5 

atom5 = 
    | {atom7 "+"} atom7

atom7 = 
    | {atom10 "*"} atom10

atom10 =
    | AtomName "(" ")"
    | AtomName "(" linkList ")"
    | AtomName
    | Integer
    | processContext  
    | listAbbreviation

processContext = "$" AtomName [ ":" type ]

type =
    | "int"
    | "string"
    | "ground"
    | "unary"

linkList =
    | {pointing_to ","} pointing_to
    | Null

pointing_to = 
    | atom
    | LinkName

indirection =
    | LinkName "->" pointing_to    

listAbbreviation =
    | "[" { atom0 ","} atom0 [ "|" atom0 ] "]"


(* "." < ":-" < ","  *)
